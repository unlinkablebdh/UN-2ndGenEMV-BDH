free cout, card, term: channel.

type key.
type sskey.
type spkey.
type point.
type scalar.

fun smult(scalar, point): point.
fun h(point): key.
fun pk(sskey): spkey.
fun sign(point , sskey): bitstring.
fun enc(bitstring, key): bitstring.

const G: point [data].

reduc forall m: bitstring, k: key; dec(enc(m, k), k) = m.
reduc forall m: point, k: sskey; check(sign(m, k), pk(k)) = m.

equation forall a: scalar, b: scalar; smult(a, smult(b, G)) = smult(b, smult(a, G)).
equation forall a: scalar, b: scalar, c: scalar; smult(a, smult(b, smult(c, G))) = smult(b, smult(a, smult(c, G))).

event snd1(point).
event rec1(point).
event snd2(point).
event rec2(point).
event cardTerm(key, bitstring).
event terminalTerm(key, bitstring).

(*check that terminalTerm is reachable*)
query k: key, z2: bitstring;
  event(terminalTerm(k, z2)).

query k: key, z2: bitstring, z1: point;
 inj-event(terminalTerm(k, z2)) ==> (inj-event(rec1(z1)) ==> inj-event(snd1(z1))).

query k: key, z2: bitstring, y: point;
 inj-event(terminalTerm(k, z2)) ==> (inj-event(rec2(y)) ==> inj-event(snd2(y))).

query k: key, z2: bitstring;
 inj-event(terminalTerm(k, z2)) ==> inj-event(cardTerm(k, z2)).

let C(s: sskey, c: scalar, ch: channel) =
  new a: scalar;
  event snd1(smult(a, smult(c, G)));
  out(ch, smult(a, smult(c, G)));
    in(ch, y: point);
    event rec2(y);  
      let k = h(smult(a, smult(c, y))) in
      let cert = (smult(c, G), sign(smult(c, G), s)) in
      event cardTerm(k, enc(((a, smult(c, G)), cert), k));
      out(ch, enc(((a, smult(c, G)), cert), k)).

let T(s: sskey, ch: channel) =
  new t: scalar;
  in(ch, z1: point);
  event rec1(z1);
    event snd2(smult(t, G));
    out(ch, smult(t, G));
      in(ch, z2: bitstring);
      let k = h(smult(t, z1)) in
      let ((n1: scalar , n2: point), (n3: point, n4: bitstring)) = dec(z2, k) in
      if (n2 = check(n4, pk(s))) && (smult(n1, n2) = z1) then
      event terminalTerm(k, z2).

(*populate system with cards*)
let PopCard(s: sskey)=
    new c: scalar;
    !(new chc: channel;
      out(card, chc);
      C(s, c, chc)).

(*populate system with terminals*)
let PopTerminal(s: sskey)=
    new cht: channel;
    out(term, cht);
    T(s, cht).

process
    new s: sskey;
    out(cout, pk(s));
    !PopCard(s) | !PopTerminal(s)